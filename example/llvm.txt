program: global_decl* EOF;

global_decl
  : NEW_LINE
  | global_var_decl
  | global_struct_decl
  | global_target_decl
  | global_srcname_decl
  | global_func_decl
  | global_func_def
  | global_attribute_def
  | global_metadata
  | global_asm_decl
  | global_type_alias
  ;

global_type_alias: IDENTIFIER EQUALS TYPE type;

global_var_decl
  : IDENTIFIER EQUALS (linkage)? (preemption)? (visibility)? (dll_storage)? (storage_model)? (UNNAMED_ADDR)? (addr_space)? (EXTERNALLY_INITIALIZED)? (GLOBAL | CONSTANT) (const | type) (COMMA decl_attr)* NEW_LINE
  | IDENTIFIER EQUALS COMDAT ANY
  ; 

global_asm_decl: MODULE ASM STRING_LITERAL;
global_metadata: meta_ident EQUALS metadata;
global_attribute_def: ATTRIBUTES NUMBERING EQUALS LBRACE function_attrib* RBRACE NEW_LINE;
global_struct_decl: IDENTIFIER EQUALS struct_type_decl NEW_LINE;
global_target_decl: TARGET (DATALAYOUT | TRIPLE) EQUALS STRING_LITERAL NEW_LINE;
global_srcname_decl: SRC_FILENAME EQUALS STRING_LITERAL NEW_LINE;
global_func_decl: DECLARE (linkage)? (preemption)? (visibility)? (dll_storage)? calling_convention? (param_attrib)* type IDENTIFIER LPAREN arg_list? RPAREN (UNNAMED_ADDR)? align? (GC STRING_LITERAL)? (PREFIX const)? (PROLOGUE const)? NUMBERING? NEW_LINE;
global_func_def: DEFINE (linkage)? (preemption)? (visibility)? (dll_storage)? calling_convention? (param_attrib)* type IDENTIFIER LPAREN arg_list? RPAREN (UNNAMED_ADDR)? addr_space? function_attrib* (SECTION STRING_LITERAL)? (PARTITION STRING_LITERAL)? (COMDAT (IDENTIFIER)?)? align? (GC STRING_LITERAL)? (PREFIX const)? (PROLOGUE const)? (PERSONALITY const)? (metadata)* NUMBERING? LBRACE NEW_LINE basic_block* RBRACE;

basic_block : (LABEL_DECL | STRING_LITERAL COLON)? (instr | NEW_LINE)+;

instr
  : ( ret_instr
   | br_instr
   | indirectbr_instr
   | invoke_instr
   | callbr_instr
   | resume_instr
   | catchswitch_instr
   | catchret_instr
   | cleanupret_instr
   | unreachable_instr
   | fneg_instr
   | add_instr
   | fadd_instr
   | sub_instr
   | fsub_instr
   | mul_instr
   | fmul_instr
   | udiv_instr
   | sdiv_instr
   | fdiv_instr
   | urem_instr
   | srem_instr
   | frem_instr
   | shl_instr
   | lshr_instr
   | ashr_instr
   | and_instr
   | or_instr
   | xor_instr
   | extractelement_instr
   | insertelement_instr
   | shufflevector_instr
   | insertvalue_instr
   | extractvalue_instr
   | alloca_instr
   | load_instr
   | store_instr
   | fence_instr
   | cmpxchg_instr
   | atomicrmw_instr
   | getelementptr_instr
   | trunc_instr
   | zext_instr
   | sext_instr
   | fptrunc_instr
   | fpext_instr
   | fptoui_instr
   | fptosi_instr
   | uitofp_instr
   | sitofp_instr
   | ptrtoint_instr
   | ptrtoaddr_instr
   | inttoptr_instr
   | bitcast_instr
   | addrspacecast_instr
   | icmp_instr
   | fcmp_instr
   | phi_instr
   | select_instr
   | freeze_instr
   | call_instr
   | asm_instr
   | vaarg_instr
   | landingpad_instr
   | catchpad_instr
   | cleanuppad_instr
   | switch_instr
) (COMMA metadata)* NEW_LINE;

ret_instr
  : RET (type IDENTIFIER | const)
  | RET VOID
  ;

br_instr
  : BR LABEL IDENTIFIER
  | BR type IDENTIFIER COMMA LABEL IDENTIFIER COMMA LABEL IDENTIFIER
  ;

calling_convention
  : CCC
  | FASTCC
  | COLDCC
  | GHCCC
  | ANYREGCC
  | PRESERVE_MOSTCC
  | PRESERVE_ALLCC
  | PRESERVE_NONECC
  | CXX_FAST_TLSCC
  | TAILCC
  | SWIFTCC
  | SWIFTTAILCC
  | CFGUARD_CHECKCC
  | STRING_LITERAL
  ; 

switch_instr: SWITCH type value COMMA LABEL IDENTIFIER LBRACK NEW_LINE? (type untyped_const COMMA LABEL IDENTIFIER NEW_LINE?)* RBRACK;

indirectbr_instr: INDIRECTBR PTR value COMMA LBRACK (LABEL IDENTIFIER (COMMA LABEL IDENTIFIER)*) RBRACK;

invoke_instr: (IDENTIFIER EQUALS)? INVOKE calling_convention? (param_attrib)* (addr_space)? fnty value LPAREN param_list RPAREN function_attrib* NEW_LINE? TO LABEL IDENTIFIER UNWIND LABEL IDENTIFIER;

callbr_instr: (IDENTIFIER EQUALS)? CALLBR calling_convention? (param_attrib)* (addr_space)? fnty value LPAREN param_list RPAREN function_attrib* TO LABEL IDENTIFIER+;

resume_instr: RESUME type value;

catchswitch_instr: (IDENTIFIER EQUALS)? CATCHSWITCH WITHIN (IDENTIFIER | NONE) (LABEL IDENTIFIER (COMMA LABEL IDENTIFIER)*)? UNWIND (TO CALLER | LABEL IDENTIFIER);

catchret_instr: CATCHRET FROM IDENTIFIER TO LABEL IDENTIFIER;

cleanupret_instr: CLEANUPRET FROM value UNWIND (LABEL IDENTIFIER | TO CALLER);

unreachable_instr: UNREACHABLE;

fneg_instr: IDENTIFIER EQUALS FNEG fast_math_flag* type value;

add_instr: IDENTIFIER EQUALS ADD ((NUW NSW) | NUW | NSW)? type value COMMA value;
fadd_instr: IDENTIFIER EQUALS FADD fast_math_flag* type value COMMA value;

sub_instr: IDENTIFIER EQUALS SUB ((NUW NSW) | NUW | NSW)? type value COMMA value;
fsub_instr: IDENTIFIER EQUALS FSUB fast_math_flag* type value COMMA value;

mul_instr: IDENTIFIER EQUALS MUL ((NUW NSW) | NUW | NSW)? type value COMMA value;
fmul_instr: IDENTIFIER EQUALS FMUL fast_math_flag* type value COMMA value;

udiv_instr: IDENTIFIER EQUALS UDIV (EXACT)? type value COMMA value;
sdiv_instr: IDENTIFIER EQUALS SDIV (EXACT)? type value COMMA value;
fdiv_instr: IDENTIFIER EQUALS FDIV fast_math_flag* type value COMMA value;

urem_instr: IDENTIFIER EQUALS UREM type value COMMA value;
srem_instr: IDENTIFIER EQUALS SREM type value COMMA value;
frem_instr: IDENTIFIER EQUALS SREM type value COMMA value;

shl_instr: IDENTIFIER EQUALS SHL ((NUW NSW) | NUW | NSW)? type value COMMA value;
lshr_instr: IDENTIFIER EQUALS LSHR (EXACT)? type value COMMA value;
ashr_instr: IDENTIFIER EQUALS ASHR (EXACT)? type value COMMA value;

and_instr: IDENTIFIER EQUALS AND type value COMMA value;
or_instr: IDENTIFIER EQUALS OR (DISJOINT)? type value COMMA value;
xor_instr: IDENTIFIER EQUALS XOR (DISJOINT)? type value COMMA value;

extractelement_instr: IDENTIFIER EQUALS EXTRACT_ELEMENT type value COMMA type value;
insertelement_instr: IDENTIFIER EQUALS INSERT_ELEMENT type value COMMA type value COMMA type value;
shufflevector_instr: IDENTIFIER EQUALS SHUFFLE_VECTOR type value COMMA type value COMMA type value;
extractvalue_instr: IDENTIFIER EQUALS EXTRACT_VALUE type value COMMA int_literal (COMMA int_literal);
insertvalue_instr: IDENTIFIER EQUALS INSERT_VALUE type value COMMA type value COMMA int_literal (COMMA int_literal)*;

alloca_instr: IDENTIFIER EQUALS ALLOCA INALLOCA? type (COMMA type value)? (COMMA align)? (COMMA addr_space)?;
load_instr: IDENTIFIER EQUALS LOAD (ATOMIC)? (VOLATILE)? type COMMA PTR (ptr_constant | IDENTIFIER) syncscope? ordering? (COMMA align)? (COMMA metadata)*;
store_instr: STORE (ATOMIC)? (VOLATILE)? type value COMMA PTR (ptr_constant | IDENTIFIER) syncscope? ordering? (COMMA align)? (COMMA metadata)*;
getelementptr_instr: IDENTIFIER EQUALS GETELEMENTPTR (INBOUNDS | NUSW | NUW | inrange)* type COMMA PTR (value) (COMMA type value)*;

fence_instr: FENCE syncscope? ordering;
cmpxchg_instr: IDENTIFIER EQUALS CMPXCHG WEAK? VOLATILE? PTR (ptr_constant | IDENTIFIER) COMMA type value COMMA type value syncscope? ordering (COMMA align)?;
atomicrmw_instr: IDENTIFIER EQUALS ATOMICRMW VOLATILE? (XCHG | ADD | SUB | AND | NAND | OR | XOR | MAX | MIN | UMAX | UMIN | FADD | FSUB | FMAX | FMIN | FMAXIMUM | FMINIMUM | UINC_WRAP | UDEC_WRAP | USUB_COND | USUB_SAT) PTR value COMMA type value syncscope? ordering (COMMA align)?;

trunc_instr: IDENTIFIER EQUALS TRUNC NUW? NSW? type value TO type;
zext_instr: IDENTIFIER EQUALS ZEXT NNEG? type value TO type;
sext_instr: IDENTIFIER EQUALS SEXT type value TO type;
fptrunc_instr: IDENTIFIER EQUALS FPTRUNC fast_math_flag* type value TO type;
fpext_instr: IDENTIFIER EQUALS FPEXT fast_math_flag* type value TO type;
fptoui_instr: IDENTIFIER EQUALS FPTOUI type value TO type;
fptosi_instr: IDENTIFIER EQUALS FPTOSI type value TO type;
uitofp_instr: IDENTIFIER EQUALS UITOFP NNEG? type value TO type;
sitofp_instr: IDENTIFIER EQUALS SITOFP type value TO type;
ptrtoint_instr: IDENTIFIER EQUALS PTRTOINT type value TO type;
ptrtoaddr_instr: IDENTIFIER EQUALS PTRTOADDR type value TO type;
inttoptr_instr: IDENTIFIER EQUALS INTTOPTR type value TO type;
bitcast_instr: IDENTIFIER EQUALS BITCAST type value TO type;
addrspacecast_instr: IDENTIFIER EQUALS ADDRSPACECAST type value TO type;

condition
  : EQ
  | NE
  | UGT
  | UGE
  | ULT
  | ULE
  | SGT
  | SGE
  | SLT
  | SLE
  | FALSE
  | OEQ
  | OGT
  | OGE
  | OLT
  | OLE
  | ONE
  | ORD
  | UEQ
  | UNE
  | UNO
  | TRUE
  ;
icmp_instr: IDENTIFIER EQUALS ICMP SAMESIGN? condition type value COMMA value;
fcmp_instr: IDENTIFIER EQUALS FCMP fast_math_flag* condition type value COMMA value;

phi_instr: IDENTIFIER EQUALS PHI fast_math_flag* type LBRACK value COMMA IDENTIFIER RBRACK (COMMA LBRACK value COMMA IDENTIFIER RBRACK)*;
select_instr: IDENTIFIER EQUALS SELECT fast_math_flag* type value COMMA type value COMMA type value;

freeze_instr: IDENTIFIER EQUALS FREEZE type value;

call_instr: (IDENTIFIER EQUALS)? (TAIL | MUSTTAIL | NOTAIL)? CALL fast_math_flag* calling_convention? param_attrib* addr_space? fnty value LPAREN param_list RPAREN function_attrib*;

asm_instr: (IDENTIFIER EQUALS)? (TAIL | MUSTTAIL | NOTAIL)? CALL fast_math_flag* type ASM param_attrib* STRING_LITERAL COMMA STRING_LITERAL LPAREN param_list RPAREN function_attrib*;

vaarg_instr: IDENTIFIER EQUALS VA_ARG type value COMMA type;

clause
  : CATCH type value
  | FILTER type value
  ;
landingpad_instr: IDENTIFIER EQUALS LANDINGPAD type NEW_LINE? CLEANUP? clause*;
catchpad_instr: IDENTIFIER EQUALS CATCHPAD WITHIN (IDENTIFIER | NONE) (LBRACK (type value (COMMA type value)*)? RBRACK)?;
cleanuppad_instr: IDENTIFIER EQUALS CLEANUPPAD WITHIN IDENTIFIER (LBRACK (type value (COMMA type value)*)? RBRACK)?;

decl_attr
  : SECTION STRING_LITERAL
  | PARTITION STRING_LITERAL
  | COMDAT (IDENTIFIER)?
  | align
  | CODE_MODEL STRING_LITERAL
  | NO_SANITIZE_ADDRESS
  | NO_SANITIZE_HWADDRESS
  | SANITIZE_ADDRESS_DYNINIT
  | SANITIZE_MEMTAG
  ;

linkage
  : PRIVATE
  | INTERNAL
  | AVAILABLE_EXTERNALLY
  | LINKONCE
  | WEAK
  | COMMON
  | APPENDING
  | EXTERN_WEAK
  | LINKONCE_ODR
  | WEAK_ODR
  | EXTERNAL
  ;

storage_model
  : LOCALDYNAMIC
  | INITIALEXEC
  | LOCALEXEC
  ;

preemption
  : DSO_PREEMPTABLE
  | DSO_LOCAL
  ;

visibility
  : DEFAULT
  | HIDDEN
  | PROTECTED
  ;

dll_storage
  : DLLIMPORT
  | DLLEXPORT
  ;

float_type
  : HALF
  | BFLOAT
  | FLOAT
  | DOUBLE
  | FP128
  | X86_FP80
  | PPC_FP128
  ;

ptr_type
  : new_ptr_style
  | old_ptr_style
  ;

vector_type: LANGLE (VSCALE XLIT)? int_literal XLIT type RANGLE ;

fnty: function_type | type;
function_type: type LPAREN param_type_list RPAREN;
param_type_list
  : DOTS
  | type COMMA param_type_list
  | type
  ;

array_type: LBRACK int_literal XLIT type RBRACK;

struct_type_decl: TYPE opaque_or_struct;
opaque_or_struct
  : OPAQUE
  | literal_struct_type
  ;

literal_struct_type
  : LBRACE type_list RBRACE
  | LANGLE LBRACE type_list RBRACE RANGLE
  ;

type
  : literal_struct_type
  | array_type
  | vector_type
  | VOID
  | INT_TYPE
  | float_type
  | ptr_type
  | METADATA
  | LABEL
  | IDENTIFIER
  ;

type_list: type (COMMA type)*;

value
  : untyped_const
  | IDENTIFIER
  | metadata
  | UNDEF
  | POISON
  ;

int_literal
  : HEXA_LITERAL
  | DECIMAL_LITERAL
  ;
float_literal
  : FLOAT_HEXA_LITERAL
  | FLOAT_LITERAL
  ;

new_ptr_style: PTR (addr_space)?;
old_ptr_style: (literal_struct_type | array_type | vector_type | INT_TYPE | float_type | IDENTIFIER) STAR (addr_space)?;
addr_space: ADDRSPACE LPAREN (STRING_LITERAL | int_literal) RPAREN;

syncscope: SYNCSCOPE LPAREN STRING_LITERAL RPAREN;
ordering: UNORDERED | MONOTONIC | ACQUIRE | RELEASE | ACQ_REL | SEQ_CST;

struct_constant
  : LBRACE const_list RBRACE
  | LANGLE LBRACE const_list RBRACE RANGLE
  ;
array_constant: LBRACK (const (COMMA const)*) RBRACK;
vector_constant: LANGLE const_list RANGLE;
ptr_constant: NULL | IDENTIFIER;

const_expr
  : TRUNC LPAREN const TO type RPAREN
  | PTRTOINT LPAREN const TO type RPAREN
  | PTRTOADDR LPAREN const TO type RPAREN
  | INTTOPTR LPAREN const TO type RPAREN
  | BITCAST LPAREN const TO type RPAREN
  | ADDRSPACECAST LPAREN const TO type RPAREN
  | GETELEMENTPTR INBOUNDS? LPAREN type COMMA const (COMMA INT_TYPE int_literal)+ RPAREN
  | EXTRACT_ELEMENT LPAREN value COMMA int_literal RPAREN
  | INSERT_ELEMENT LPAREN value COMMA value COMMA int_literal RPAREN
  | SHUFFLE_VECTOR LPAREN value COMMA value COMMA value RPAREN
  | ADD LPAREN value COMMA value RPAREN
  | SUB LPAREN value COMMA value RPAREN
  | XOR LPAREN value COMMA value RPAREN
  ;

untyped_const
  :  float_literal
   | int_literal
   | struct_constant
   | array_constant
   | vector_constant
   | STRING_LITERAL
   | ptr_constant
   | ZEROINITIALIZER
   | METADATA metadata
   | const_expr
   ;

const
  :  (float_type float_literal)
   | (INT_TYPE int_literal) 
   | (literal_struct_type struct_constant)
   | (array_type array_constant)
   | (vector_type vector_constant)
   | (array_type STRING_LITERAL)
   | (ptr_type ptr_constant)
   | (type const_expr)
   | (type ZEROINITIALIZER) 
   | (METADATA metadata)
   ;

const_list: const (COMMA const)*;

by_val: BYVAL LPAREN type RPAREN;
by_ref: BYREF LPAREN type RPAREN;
preallocated: PREALLOC LPAREN type RPAREN;
inalloca: INALLOCA LPAREN type RPAREN;
sret: SRET LPAREN type RPAREN;
elementtype: ELEMENTTYPE LPAREN type RPAREN;
align
  : ALIGN int_literal
  | ALIGN LPAREN int_literal RPAREN;
capture_el: NONE | ADDRESS | PROVENANCE | ADDRESS_IS_NULL | READ_PROVENANCE | RET COLON ADDRESS;
capture_el_list: capture_el (COMMA capture_el)*;
captures: CAPTURES LPAREN capture_el_list RPAREN;
dereferenceable: DEREFERENCEABLE LPAREN int_literal RPAREN;
dereferenceable_or_null: DEREFERENCEABLE_OR_NULL LPAREN int_literal RPAREN;
test_mask: NAN | INF | NORM | SUB | ZERO | ALL | SNAN | QNAN | NINF | NNORM | NSUB | NZERO | PZERO | PSUB | PNORM | PINF;
nofpclass: NOFPCLASS LPAREN test_mask RPAREN;
alignstack: ALIGNSTACK LPAREN int_literal RPAREN;
initialize_el: LPAREN int_literal COMMA int_literal RPAREN;
initialize_el_list: initialize_el (COMMA initialize_el)*;
initializes: INITIALIZES LPAREN initialize_el_list RPAREN;
range: RANGE LPAREN const COMMA untyped_const RPAREN;
allocfamily: STRING_LITERAL EQUALS STRING_LITERAL;
uwtable: UWTABLE (LPAREN (SYNC | ASYNC) RPAREN)?;
allockind: ALLOCKIND LPAREN STRING_LITERAL RPAREN;
allocsize: ALLOCSIZE LPAREN int_literal (COMMA int_literal)* RPAREN;
memory_access: NONE | READ | WRITE | READWRITE | ERRNOMEM;
memory_access_decl
  : (ARGMEM COLON)? memory_access
  | (INACCESSIBLEMEM COLON)? memory_access
  ;
memory: MEMORY LPAREN (memory_access_decl (COMMA memory_access_decl)*) RPAREN;
vscale_range: VSCALE_RANGE LPAREN int_literal (COMMA int_literal)? RPAREN;

param_attrib
  : ZEROEXT
  | SIGNEXT
  | NOEXT
  | INREG
  | by_val
  | by_ref
  | preallocated
  | inalloca
  | sret
  | elementtype
  | align
  | NOALIAS
  | captures
  | NOCAPTURE
  | NOFREE
  | NEST
  | RETURNED
  | NONNULL
  | dereferenceable
  | dereferenceable_or_null
  | SWIFTSELF
  | SWIFTASYNC
  | SWIFTERROR
  | IMMARG
  | NOUNDEF
  | nofpclass
  | alignstack
  | ALLOCALIGN
  | ALLOCPTR
  | READNONE
  | READONLY
  | WRITEONLY
  | WRITABLE
  | initializes
  | DEAD_ON_UNWIND
  | DEAD_ON_RETURN
  | SIDEEFFECT
  | UNWIND
  | INTELDIALECT
  | range
  ;

function_attrib
  : alignstack
  | allocfamily
  | allockind
  | allocsize
  | ALWAYSINLINE
  | BUILTIN
  | COLD
  | CONVERGENT
  | DISABLE_SANITIZER_INSTRUMENTATION
  | STRING_LITERAL
  | FN_RET_THUNK_EXTERN
  | HOT
  | INLINEHINT
  | JUMPTABLE
  | memory
  | MINSIZE
  | NAKED
  | NOJUMPTABLES
  | NOBUILTIN
  | NOCALLBACK
  | NODIVERGENCESOURCE
  | NODUPLICATE
  | NOFREE
  | NOIMPLICITFLOAT
  | NOINLINE
  | NOMERGE
  | NONLAZYBIND
  | NOPROFILE
  | SKIPPROFILE
  | NOREDZONE
  | INDIRECTTLSSEGREFS
  | NORETURN
  | NORECURSE
  | WILLRETURN
  | NOSYNC
  | NOUNWIND
  | NOSANITIZEBOUNDS
  | NOSANITIZECOVERAGE
  | NULL_POINTER_IS_VALID
  | OPTDEBUG
  | OPTFORFUZZING
  | OPTNONE
  | OPTSIZE
  | RETURNSTWICE
  | SAFESTACK
  | SANITIZE_ADDRESS
  | SANITIZE_MEMORY
  | SANITIZE_THREAD
  | SANITIZE_HWADDRESS
  | SANITIZE_MEMTAG
  | SANITIZE_REALTIME
  | SANITIZE_REALTIME_BLOCKING
  | SANITIZE_ALLOC_TOKEN
  | SPECULATIVE_LOAD_HARDENING
  | SPECULATABLE
  | SSP
  | SSPSTRONG
  | SSPREQ
  | STRICTFP
  | SIDEEFFECT
  | uwtable
  | NOCF_CHECK
  | SHADOWCALLSTACK
  | MUSTPROGRESS
  | vscale_range
  | NOCREATEUNDEFORPOISON
  | VECTOR_FUNCTION_ABI_VARIANT
  | preallocated
  | NUMBERING
  ;

arg_decl
  : DOTS
  | type (param_attrib)* IDENTIFIER?
  ;
arg_list: arg_decl (COMMA arg_decl)*;

param_decl: type (param_attrib)* value;
param_list: param_decl* (COMMA param_decl)*;

inrange: INRANGE LPAREN int_literal COMMA int_literal RPAREN;

meta_ident: meta_flag | meta_string | meta_number | meta_tuple;
meta_flag: 
  EXCLAM (DBG | NONTEMPORAL | LLVM_LOOP | INVARIANT_GROUP | LLVM_ACCESS_GROUP | IRR_LOOP | INVARIANT_GROUP | CALLEE_TYPE | ASSOCIATED | PROF | ANNOTATION | FUNC_SANITIZE | KCFI_TYPE | MEMPROF | CALLSITE | NOALIAS | NOALIAS_ADDRSPACE | MMRA | ALLOC_TOKEN | NOUNDEF | ALIGN | DEREFERENCEABLE | DEREFERENCEABLE_OR_NULL | NONNULL | TBAA | ALIASSCOPE | LLVM_IDENT | LLVM_MODULE_FLAGS | CALLEES | SRCLOC);
meta_string: DISTINCT? EXCLAM STRING_LITERAL;
meta_number: DISTINCT? EXCLAM int_literal;
meta_tuple: DISTINCT? EXCLAM LBRACE ((metadata|const) (COMMA (metadata|const))*)? RBRACE;
meta_node: DISTINCT? meta_flag metadata;
metadata
  : meta_string
  | meta_tuple
  | meta_number
  | meta_node
  ;

STRING_LITERAL: ('c')? '"' [^"]* '"';
CHAR_LITERAL: '\'' [^'] '\'';
DECIMAL_LITERAL: ('-')? (([1-9] [0-9]*) | '0' | 'true' | 'false');
HEXA_LITERAL: [us] '0' [xX] [0-9a-fA-F]+;
FLOAT_LITERAL: ('-')?[0-9]*'.'[0-9]+('e'('+'|'-')[0-9]+)?;
FLOAT_HEXA_LITERAL: '0' [xX] [MLHR]? [0-9a-fA-F]+;
IDENTIFIER: ([%@$][-a-zA-Z$._0-9]+)|([%@$] '"' [^"]* '"');
LABEL_DECL: [-A-Za-z0-9._]+ ':' (' '|'\t')* (';' [^\r\n]*)? [\r\n]+;

fast_math_flag
  : FAST
  | NNAN
  | NINF
  | NSZ
  ;

FAST: 'fast';
NNAN: 'nnan';
NINF: 'ninf';
NSZ: 'nsz';
EXACT: 'exact';
NUW: 'nuw';
NSW: 'nsw';
NUSW: 'nusw';

CALLEES: 'callees';
MODULE: 'module';
ASM: 'asm';
PRIVATE: 'private';
INTERNAL: 'internal';
AVAILABLE_EXTERNALLY: 'available_externally';
LINKONCE: 'linkonce';
WEAK: 'weak';
COMMON: 'common';
APPENDING: 'appending';
EXTERN_WEAK: 'extern_weak';
LINKONCE_ODR: 'linkonce_odr';
WEAK_ODR: 'weak_odr';
EXTERNAL: 'external';
ATOMIC: 'atomic';
SYNCSCOPE: 'syncscope';
UNORDERED: 'unordered';
MONOTONIC: 'monotonic';
ACQUIRE: 'acquire';
RELEASE: 'release';
ACQ_REL: 'acq_rel';
SEQ_CST: 'seq_cst';
TBAA: 'tbaa';
ALIASSCOPE: 'alias.scope';
SRCLOC: 'srcloc';

CONSTANT: 'constant';
GLOBAL: 'global';
UNDEF: 'undef';
ZEROINITIALIZER: 'zeroinitializer';
METADATA: 'metadata';
DISTINCT: 'distinct'; 
ATTRIBUTES: 'attributes';

LLVM_MODULE_FLAGS: 'llvm.module.flags';
LLVM_IDENT: 'llvm.ident';
DBG: 'dbg';
LLVM_LOOP: 'llvm.loop';
LLVM_ACCESS_GROUP: 'llvm.access.group';
IRR_LOOP: 'irr_loop';
INVARIANT_GROUP: 'invariant.group';
CALLEE_TYPE: 'callee_type';
ASSOCIATED: 'associated';
PROF: 'prof';
ANNOTATION: 'annotation';
FUNC_SANITIZE: 'func_sanitize';
KCFI_TYPE: 'kcfi_type';
MEMPROF: 'memprof';
CALLSITE: 'callsite';
NOALIAS_ADDRSPACE: 'noalias.addrspace';
MMRA: 'mmra';
ALLOC_TOKEN: 'alloc_token';
NONTEMPORAL: 'nontemporal';
INVARIANT_LOAD: 'invariant.load';

LOCALDYNAMIC: 'localdynamic';
INITIALEXEC: 'initialexec';
LOCALEXEC: 'localexec';

DSO_PREEMPTABLE: 'dso_preemptable';
DSO_LOCAL: 'dso_local';

VOID: 'void';
INT_TYPE: 'i' [0-9]+ ;
NUMBERING: '#' [0-9]* ;
HALF : 'half';
BFLOAT : 'bfloat';
FLOAT : 'float';
DOUBLE : 'double';
FP128 : 'fp128';
X86_FP80 : 'x86_fp80';
PPC_FP128 : 'ppc_fp128';

UNNAMED_ADDR: 'unnamed_addr' | 'local_unnamed_addr';

DEFAULT: 'default';
HIDDEN: 'hidden';
PROTECTED: 'protected';

DLLIMPORT: 'dllimport';
DLLEXPORT: 'dllexport';

EXTERNALLY_INITIALIZED: 'externally_initialized';

CCC: 'ccc';
FASTCC: 'fastcc';
COLDCC: 'coldcc';
GHCCC: 'ghccc';
ANYREGCC: 'anyregcc';
PRESERVE_MOSTCC: 'preserve_mostcc';
PRESERVE_ALLCC: 'preserve_allcc';
PRESERVE_NONECC: 'preserve_nonecc';
CXX_FAST_TLSCC: 'cxx_fast_tlscc';
TAILCC: 'tailcc';
SWIFTCC: 'swiftcc';
SWIFTTAILCC: 'swifttailcc';
CFGUARD_CHECKCC: 'cfguard_checkcc';

TYPE: 'type';
OPAQUE: 'opaque';
PTR: 'ptr';
ADDRSPACE: 'addrspace';
TARGET: 'target';
SRC_FILENAME: 'source_filename';
DATALAYOUT: 'datalayout';
TRIPLE: 'triple';
DECLARE: 'declare';
DEFINE: 'define';
ZEROEXT: 'zeroext';
SIGNEXT: 'signext';
NOEXT: 'noext';
INREG: 'inreg';
BYVAL: 'byval';
BYREF: 'byref';
PREALLOC: 'preallocated';
INALLOCA: 'inalloca';
SRET: 'sret';
ELEMENTTYPE: 'elementtype';
NOALIAS: 'noalias';
CAPTURES: 'captures';
NOCAPTURE: 'nocapture';
SIDEEFFECT: 'sideeffect';
NEST: 'nest';
RETURNED: 'returned';
NONNULL: 'nonnull';
DEREFERENCEABLE: 'dereferenceable';
DEREFERENCEABLE_OR_NULL: 'dereferenceable_or_null';
SWIFTSELF: 'swiftself';
SWIFTASYNC: 'swiftasync';
SWIFTERROR: 'swifterror';
IMMARG: 'immarg';
NOUNDEF: 'noundef';
POISON: 'poison';
NOFPCLASS: 'nofpclass';
NAN: 'nan';
NONE: 'none';
ADDRESS: 'address';
PROVENANCE: 'provenance';
ADDRESS_IS_NULL: 'address_is_null';
READ_PROVENANCE: 'read_provenance';
PERSONALITY: 'personality';
INF: 'inf';
NORM: 'norm';
ZERO: 'zero';
FROM: 'from';
ALL: 'all';
SNAN: 'snan';
QNAN: 'qnan';
NNORM: 'nnorm';
NSUB: 'nsub';
NZERO: 'nzero';
PZERO: 'pzero';
PSUB: 'psub';
PNORM: 'pnorm';
PINF: 'pinf';
ALIGNSTACK: 'alignstack';
ALLOCALIGN: 'allocalign';
ALLOCKIND: 'allockind';
ALLOCSIZE: 'allocsize';
ALLOCPTR: 'allocptr';
ALWAYSINLINE: 'alwaysinline';
BUILTIN: 'builtin';
COLD: 'cold';
CONVERGENT: 'convergent';
DISABLE_SANITIZER_INSTRUMENTATION: 'disable_sanitizer_instrumentation';
FN_RET_THUNK_EXTERN: 'fn_ret_thunk_extern';
HOT: 'hot';
INLINEHINT: 'inlinehint';
JUMPTABLE: 'jumptable';
MEMORY: 'memory';
READ: 'read';
WRITE: 'write';
READWRITE: 'readwrite';
READNONE: 'readnone';
READONLY: 'readonly';
WRITEONLY: 'writeonly';
WRITABLE: 'writable';
ARGMEM: 'argmem';
INACCESSIBLEMEM: 'inaccessiblemem';
ERRNOMEM: 'errnomem';
TARGETMEM: 'targetmem';
MINSIZE: 'minsize';
NAKED: 'naked';
NOJUMPTABLES: 'no-jump-tables';
NOBUILTIN: 'nobuiltin';
NOCALLBACK: 'nocallback';
NODIVERGENCESOURCE: 'nodivergencesource';
NODUPLICATE: 'noduplicate';
NOFREE: 'nofree';
NOIMPLICITFLOAT: 'noimplicitfloat';
NOINLINE: 'noinline';
NOMERGE: 'nomerge';
NONLAZYBIND: 'nonlazybind';
NOPROFILE: 'noprofile';
SKIPPROFILE: 'skipprofile';
NOREDZONE: 'noredzone';
INDIRECTTLSSEGREFS: 'indirect-tls-seg-refs';
NORETURN: 'noreturn';
NORECURSE: 'norecurse';
WILLRETURN: 'willreturn';
NOSYNC: 'nosync';
NOUNWIND: 'nounwind';
NOSANITIZEBOUNDS: 'nosanitize_bounds';
NOSANITIZECOVERAGE: 'nosanitize_coverage';
NULL_POINTER_IS_VALID: 'null_pointer_is_valid';
OPTDEBUG: 'optdebug';
OPTFORFUZZING: 'optforfuzzing';
OPTNONE: 'optnone';
OPTSIZE: 'optsize';
RETURNSTWICE: 'returns_twice';
SAFESTACK: 'safestack';
SANITIZE_ADDRESS: 'sanitize_address';
SANITIZE_MEMORY: 'sanitize_memory';
SANITIZE_THREAD: 'sanitize_thread';
SANITIZE_HWADDRESS: 'sanitize_hwaddress';
SANITIZE_REALTIME: 'sanitize_realtime';
SANITIZE_REALTIME_BLOCKING: 'sanitize_realtime_blocking';
SANITIZE_ALLOC_TOKEN: 'sanitize_alloc_token';
SANITIZE_ADDRESS_DYNINIT: 'sanitize_address_dyninit';
SANITIZE_MEMTAG: 'sanitize_memtag';
NO_SANITIZE_ADDRESS: 'no_sanitize_address';
NO_SANITIZE_HWADDRESS: 'no_sanitize_hwaddress';
SPECULATIVE_LOAD_HARDENING: 'speculative_load_hardening';
SPECULATABLE: 'speculatable'; 
SSP: 'ssp';
SSPSTRONG: 'sspstrong';
SSPREQ: 'sspreq';
STRICTFP: 'strictfp';
UWTABLE: 'uwtable';
SYNC: 'sync';
ASYNC: 'async';
NOCF_CHECK: 'nocf_check';
SHADOWCALLSTACK: 'shadowcallstack';
MUSTPROGRESS: 'mustprogress';
VSCALE_RANGE: 'vscale_range';
NOCREATEUNDEFORPOISON: 'nocreateundeforpoison';
VECTOR_FUNCTION_ABI_VARIANT: 'vector-function-abi-variant';
INITIALIZES: 'initializes';
DEAD_ON_UNWIND: 'dead_on_unwind';
DEAD_ON_RETURN: 'dead_on_return';
RANGE: 'range';
SECTION: 'section';
PARTITION: 'partition';
COMDAT: 'comdat';
ALIGN: 'align';
CODE_MODEL: 'code_model';
VSCALE: 'vscale';
PREFIX: 'prefix';
PROLOGUE: 'prologue';
GC: 'gc';
LABEL: 'label';
RET: 'ret';
BR: 'br';
SWITCH: 'switch';
INDIRECTBR: 'indirectbr';
INVOKE: 'invoke';
TO: 'to';
UNWIND: 'unwind';
INTELDIALECT: 'inteldialect';
CALLBR: 'callbr';
RESUME: 'resume';
CATCHSWITCH: 'catchswitch';
WITHIN: 'within';
CALLER: 'caller';
CATCHRET: 'catchret';
CLEANUPRET: 'cleanupret';
UNREACHABLE: 'unreachable';
FNEG: 'fneg';
ADD: 'add';
FADD: 'fadd';
SUB: 'sub';
FSUB: 'fsub';
MUL: 'mul';
FMUL: 'fmul';
UDIV: 'udiv';
SDIV: 'sdiv';
FDIV: 'fdiv';
UREM: 'urem';
SREM: 'srem';
FREM: 'frem';
SHL: 'shl';
LSHR: 'lshr';
ASHR: 'ashr';
AND: 'and';
OR: 'or';
DISJOINT: 'disjoint';
XOR: 'xor';
EXTRACT_ELEMENT: 'extractelement';
INSERT_ELEMENT: 'insertelement';
SHUFFLE_VECTOR: 'shufflevector';
EXTRACT_VALUE: 'extractvalue';
INSERT_VALUE: 'insertvalue';
ALLOCA: 'alloca';
LOAD: 'load';
STORE: 'store';
VOLATILE: 'volatile';
FENCE: 'fence';
CMPXCHG: 'cmpxchg';
ATOMICRMW: 'atomicrmw';
GETELEMENTPTR: 'getelementptr';
INBOUNDS: 'inbounds';
INRANGE: 'inrange';
XCHG: 'xchg';
NAND: 'nand';
MAX: 'max';
MIN: 'min';
UMAX: 'umax';
UMIN: 'umin';
FMAX: 'fmax';
FMIN: 'fmin';
FMAXIMUM: 'fmaximum';
FMINIMUM: 'fminimum';
UINC_WRAP: 'uinc_wrap';
UDEC_WRAP: 'udec_wrap';
USUB_COND: 'usub_cond';
USUB_SAT: 'usub_sat';
TRUNC: 'trunc';
ZEXT: 'zext';
SEXT: 'sext';
FPTRUNC: 'fptrunc';
FPEXT: 'fpext';
FPTOUI: 'fptoui';
FPTOSI: 'fptosi';
UITOFP: 'uitofp';
SITOFP: 'sitofp';
PTRTOINT: 'ptrtoint';
PTRTOADDR: 'ptrtoaddr';
INTTOPTR: 'inttoptr';
BITCAST: 'bitcast';
ADDRSPACECAST: 'addrspacecast';
ICMP: 'icmp';
FCMP: 'fcmp';
EQ: 'eq';
NE: 'ne';
UGT: 'ugt';
UGE: 'uge';
ULT: 'ult';
ULE: 'ule';
SGT: 'sgt';
SGE: 'sge';
SLT: 'slt';
SLE: 'sle';
FALSE: 'false';
OEQ: 'oeq';
OGT: 'ogt';
OGE: 'oge';
OLT: 'olt';
OLE: 'ole';
ONE: 'one';
ORD: 'ord';
UEQ: 'ueq';
UNE: 'une';
UNO: 'uno';
TRUE: 'true';
PHI: 'phi';
SAMESIGN: 'samesign';
SELECT: 'select';
FREEZE: 'freeze';
CALL: 'call';
VA_ARG: 'va_arg';
LANDINGPAD: 'landingpad';
CATCHPAD: 'catchpad';
CLEANUPPAD: 'cleanuppad';
TAIL: 'tail';
MUSTTAIL: 'musttail';
NOTAIL: 'notail';
CLEANUP: 'cleanup';
CATCH: 'catch';
FILTER: 'filter';
NNEG: 'nneg';
ANY: 'any';

NEW_LINE: (';' [^\r\n]*)? [\r\n]+;

@skip
SPACE: ( ' ' | '\t' )*;

@skip
LINE_COMMENT: ';' [^\r\n]*;

COLON: ':';
COMMA: ',';
DOT: '.';
EQUALS: '=';
EXCLAM: '!';
QUEST: '?';
SEMICOLON: ';';

LBRACE: '{';
LBRACK: '[';
LPAREN: '(';
RBRACE: '}';
RBRACK: ']';
RPAREN: ')';
LANGLE: '<';
RANGLE: '>';
DOTS: '...';
STAR: '*';
XLIT: [xX];
NULL: 'null';
